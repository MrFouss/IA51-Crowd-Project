/** 
 * MIT License
 * 
 * Copyright (c) 2017 Fouss
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
package fr.utbm.info.ia51.crowdproject.^agent

import fr.utbm.info.ia51.crowdproject.environment.AgentBody
import fr.utbm.info.ia51.crowdproject.environment.EnvironmentBody
import fr.utbm.info.ia51.crowdproject.environment.LevelDesign
import fr.utbm.info.ia51.crowdproject.environment.Levels
import fr.utbm.info.ia51.crowdproject.environment.SoundBeaconBody
import fr.utbm.info.ia51.crowdproject.environment.StaticSoundSource
import fr.utbm.info.ia51.crowdproject.motion.ActionQuery
import fr.utbm.info.ia51.crowdproject.motion.CryType
import fr.utbm.info.ia51.crowdproject.motion.MotionBehavior
import fr.utbm.info.ia51.crowdproject.ui.FXMLViewerController
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Destroy
import io.sarl.core.Initialize
import io.sarl.core.Lifecycle
import io.sarl.core.Logging
import io.sarl.util.OpenEventSpace
import java.util.ArrayList
import java.util.LinkedList
import java.util.Random
import java.util.TreeMap
import java.util.UUID
import org.dyn4j.geometry.Vector2
import fr.utbm.info.ia51.crowdproject.environment.HamsterBody

/**
 * The environment agent, managing all the other agents
 */
agent Environment {

	uses Logging, Lifecycle, DefaultContextInteractions, EnvironmentManager, EnvironmentFrontEnd

	val agentBodies = new TreeMap<UUID, AgentBody>
	var levelDesign : LevelDesign

	val actions = new LinkedList<ActionQuery>
	var targetPosition : Vector2

	var motionBehavior : MotionBehavior
	var time = 0 as long
	val random = new Random

	var soundUser : StaticSoundSource

	on Initialize {

		var width = occurrence.parameters.get(0) as Float
		var height = occurrence.parameters.get(1) as Float
		var nbHamsters = occurrence.parameters.get(2) as Integer
		var frustrumRadius = occurrence.parameters.get(3) as Float
		var frustrumAngle = occurrence.parameters.get(4) as Float
		motionBehavior = occurrence.parameters.get(5) as MotionBehavior
		var controller = occurrence.parameters.get(6) as FXMLViewerController
		levelDesign = new LevelDesign(occurrence.parameters.get(7) as Levels, width, height)

		// Communication from environment to UI
		(defaultSpace as OpenEventSpace).register(controller)
		// Communication from UI to environment
		controller.openSpace = defaultSpace as OpenEventSpace
		
		info("Beginning of the simulation. Spawning the environment agent.")

		setSkill(new DefaultEnvironmentManager(width, height), typeof(EnvironmentManager))
		setSkill(new DefaultEnvironmentFrontEnd, typeof(EnvironmentFrontEnd))
		
		// Add static bodies to the world
		for(s : this.levelDesign.staticBodies) {
			world.addBody(s)
		}

		// Add sound sources to the world
		for (s : this.levelDesign.soundSources) {
			world.addBody(s)
		}
	
		// Spawn hamsters
		for (i : 0 ..< nbHamsters) {
			var isDeaf = random.nextBoolean
			var agentBody = createHamster(motionBehavior, if(isDeaf) frustrumRadius else 5.0f, frustrumAngle)
			agentBodies.put(agentBody.agentId, agentBody)
			var params = new ArrayList<Object>
			params += #[agentBody.agentId, motionBehavior]
			spawnInContextWithID(typeof(Hamster), agentBody.agentId, defaultContext, params)
		}
		
		info("Number of bodies in the world: " + world.bodyCount)
		
		val totalListOfBodies = new ArrayList<EnvironmentBody>
		totalListOfBodies += levelDesign.soundSources
		totalListOfBodies += agentBodies.values
		totalListOfBodies += levelDesign.staticBodies
		emit(new FrontEndEnvironmentInitialization(totalListOfBodies))
		
		emit(new RunBeginningOfStep) [it.isMe]
	}

	on Destroy {
		info("End of the simulation. Killing the environment agent.")
	}

	on UserInteractionSoundSource {
		synchronized (this) {
			if(soundUser !== null) {
				val soundSourcesToRemove = new ArrayList<EnvironmentBody>
				soundSourcesToRemove += soundUser
				world.removeBody(soundUser)
			}
			soundUser = new SoundBeaconBody(CryType.POSITIVE, new Vector2(occurrence.x, occurrence.y))
			world.addBody(soundUser)
		}
	}

	on RunBeginningOfStep {
		synchronized (this) {

			for (e : getPerceptions(agentBodies.values).entrySet) {
				val agentId = e.key.agentId
				sendPerceptions(agentId, e.key, this.time, e.value)
			}
		}
	}
	
	on Action [occurrence.time >= this.time] {
		synchronized (this) {
			this.actions += occurrence.motion

			if (this.actions.size == this.agentBodies.size) {
				val actionsToApply = new LinkedList(actions)
				this.actions.clear
				emit(new RunEndOfStep(actionsToApply)) [it.isMe]
			}
		}
	}
	
	on RunEndOfStep {
		synchronized (this) {
			// Eventually take into account the actions of the player
			// actions += ...
			
			// Change the state of the universe
			applyActions(occurrence.actionsToApply, this.agentBodies)

			updateEnvironment

			if (isTargetReached(agentBodies.values, targetPosition)) {
				// TODO do something to notify that all hamsters reached the target
				killMe
			}

			val soundSourcesToRemove = new ArrayList<EnvironmentBody>
			this.levelDesign.soundSources.forEach[soundSource |
				if (!soundSource.update) {
					soundSourcesToRemove += soundSource
				}
			]
			this.levelDesign.soundSources.removeAll(soundSourcesToRemove)
			emit(new FrontEndEnvironmentClean(soundSourcesToRemove))
			
			// Informs the view that a front end update is needed
			val bodiesToVisuallyUpdate = new ArrayList<EnvironmentBody>()
			bodiesToVisuallyUpdate += (agentBodies.values)
			bodiesToVisuallyUpdate += this.levelDesign.soundSources

			emit(new FrontEndEnvironmentChange(bodiesToVisuallyUpdate))
			Thread.sleep(100/6)
			
			// Increase time value
			time++

			// Move to next step
			emit(new RunBeginningOfStep) [it.isMe]
		}
	}

	on EndOfSimulation {
		killMe
	}
}
