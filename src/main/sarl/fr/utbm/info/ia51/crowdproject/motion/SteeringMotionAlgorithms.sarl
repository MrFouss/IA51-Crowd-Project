/** 
 * MIT License
 * 
 * Copyright (c) 2017 Fouss
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
package fr.utbm.info.ia51.crowdproject.motion

import java.util.UUID
import org.dyn4j.geometry.Vector2

/** 
 * Steering implementation of the wandering motion behavior
 */
class SteeringWanderAlgorithm implements WanderAlgorithm {

	val WANDER_CIRCLE_DISTANCE = 2.0f
	val WANDER_CIRCLE_RADIUS = 1.0f

	override run(
		emitter : UUID,
		orientation : double,
		actualLinearVelocity : Vector2,
		actualangularVelocity : double,
		maxLinearVelocity : float,
		maxAngularVelocity : float,
		maxLinearAcceleration : float,
		maxAngularAcceleration : float
	) : ActionQuery {
		
		// Calculate the circle center
		var center = Vector2.create(WANDER_CIRCLE_DISTANCE, orientation)
		
		// Calculate the displacement and its angle
		var displacement = Vector2.create(WANDER_CIRCLE_RADIUS, orientation)
		var angle = ((Math.random - Math.random) * Math.PI*maxAngularAcceleration) as float
		if ((actualangularVelocity < 0 && angle < 0 && actualangularVelocity < -maxAngularVelocity) ||
			actualangularVelocity > 0 && angle > 0 && actualangularVelocity > maxAngularVelocity) {
			angle = -angle;
		}
		
		displacement.rotate(angle)
		
		// Calculate the wandering force and change the speed
		var linearVector = center.add(displacement)
		linearVector.magnitude = maxLinearAcceleration		

		if (actualLinearVelocity.magnitude >= maxLinearVelocity) {
			linearVector = linearVector.difference(actualLinearVelocity)
		} 
		return new ActionQuery(emitter, MotionBehavior::STEERING, linearVector, angle)
	}
}

/** 
 * Steering implementation of the seeking motion behavior
 */
class SteeringSeekAlgorithm implements SeekAlgorithm {

	def run(
		emitter : UUID,
		orientation : double,
		targetRelativePosition : Vector2,
		actualLinearVelocity : Vector2,
		actualangularVelocity : double,
		maxLinearVelocity : float,
		maxAngularVelocity : float,
		maxLinearAcceleration : float,
		maxAngularAcceleration : float
	) : ActionQuery {

		val currentVelocityVector = Vector2.create(1.0f, orientation)
		val angleBetweenVectors = currentVelocityVector.getAngleBetween(targetRelativePosition)
		val linearSpeed = maxLinearVelocity * ((Math.PI - Math.abs(angleBetweenVectors)) / Math.PI)
		val linearVelocity = Vector2.create(linearSpeed, orientation)

		if (targetRelativePosition.magnitude <= SLOW_DOWN_DISTANCE)
			linearVelocity.magnitude = linearVelocity.magnitude *
				(1 - ((SLOW_DOWN_DISTANCE - targetRelativePosition.magnitude) / SLOW_DOWN_DISTANCE))

		var angularVelocity : float
		if (Math.abs(angleBetweenVectors) <= SLOW_DOWN_ANGLE)
			angularVelocity = (angleBetweenVectors * maxAngularVelocity / SLOW_DOWN_ANGLE) as float
		else
			angularVelocity = (Math.signum(angleBetweenVectors) * maxAngularVelocity) as float

		return new ActionQuery(emitter, MotionBehavior::STEERING, linearVelocity, angularVelocity)
	}
}

/** 
 * Steering implementation of the fleeing motion behavior
 */
class SteeringFleeAlgorithm implements FleeAlgorithm {

	def run(
		emitter : UUID,
		orientation : double,
		targetRelativePosition : Vector2,
		actualLinearVelocity : Vector2,
		actualangularVelocity : double,
		maxLinearVelocity : float,
		maxAngularVelocity : float,
		maxLinearAcceleration : float,
		maxAngularAcceleration : float
	) : ActionQuery {
		val currentVelocityVector = Vector2.create(1.0f, orientation)
		val angleBetweenVectors = currentVelocityVector.getAngleBetween(targetRelativePosition)
		val linearSpeed = maxLinearVelocity * ((Math.PI - Math.abs(angleBetweenVectors)) / Math.PI)
		val linearVelocity = Vector2.create(linearSpeed, orientation)

		var angularVelocity : float
		if (Math.abs(angleBetweenVectors) <= SLOW_DOWN_ANGLE)
			angularVelocity = (angleBetweenVectors * maxAngularVelocity / SLOW_DOWN_ANGLE) as float
		else
			angularVelocity = (Math.signum(angleBetweenVectors) * maxAngularVelocity) as float

		return new ActionQuery(emitter, MotionBehavior::STEERING, linearVelocity, angularVelocity)
	}
}
