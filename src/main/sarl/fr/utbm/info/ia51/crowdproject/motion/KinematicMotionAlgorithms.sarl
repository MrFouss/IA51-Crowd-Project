/** 
 * MIT License
 * 
 * Copyright (c) 2017 Fouss
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
package fr.utbm.info.ia51.crowdproject.motion

import org.dyn4j.geometry.Vector2
import java.util.UUID

/** 
 * Kinematic implementation of the wandering motion behavior
 */
class KinematicWanderAlgorithm implements WanderAlgorithm {

	override run(emitter : UUID,
		orientation : double,
		maxLinear : float,
		maxAngular : float
	) : ActionQuery {
		
		var velocity = Vector2.create(maxLinear * 0.75f, orientation)
		var angle = ((Math.random - Math.random) * maxAngular) as float
		return new ActionQuery(emitter, MotionBehavior::KINEMATIC, velocity, angle)
	}
}

/** 
 * Kinematic implementation of the seeking motion behavior
 */
class KinematicSeekAlgorithm implements SeekAlgorithm {

	def run(
		emitter : UUID,
		orientation : double,
		targetRelativePosition : Vector2,
		maxLinear : float,
		maxAngular : float
	) : ActionQuery {
		
		val currentVelocityVector = Vector2.create(1.0f, orientation)
		val angleBetweenVectors = currentVelocityVector.getAngleBetween(targetRelativePosition)
		val linearSpeed = maxLinear * ((Math.PI - Math.abs(angleBetweenVectors)) / Math.PI)
		val linearVelocity = Vector2.create(linearSpeed, orientation)
		
		if (targetRelativePosition.magnitude <= SLOW_DOWN_DISTANCE)
			linearVelocity.magnitude = linearVelocity.magnitude * (1- ((SLOW_DOWN_DISTANCE - targetRelativePosition.magnitude) / SLOW_DOWN_DISTANCE))

		var angularVelocity : float		
		if (Math.abs(angleBetweenVectors) <= SLOW_DOWN_ANGLE)
			angularVelocity = (angleBetweenVectors * maxAngular / SLOW_DOWN_ANGLE) as float
		else
			angularVelocity = (Math.signum(angleBetweenVectors) * maxAngular) as float

		return new ActionQuery(emitter, MotionBehavior::KINEMATIC, linearVelocity, angularVelocity)
	}
}
/** 
 * Kinematic implementation of the fleeing motion behavior
 */
class KinematicFleeAlgorithm implements FleeAlgorithm {

	def run(
		emitter : UUID,
		orientation : double,
		targetRelativePosition : Vector2,
		maxLinear : float,
		maxAngular : float
	) : ActionQuery {
		
		val fleeingVector = targetRelativePosition.negative

		val currentVelocityVector = Vector2.create(1.0f, orientation)
		val angleBetweenVectors = currentVelocityVector.getAngleBetween(fleeingVector)
		val linearSpeed = maxLinear * ((Math.PI - Math.abs(angleBetweenVectors)) / Math.PI)
		val linearVelocity = Vector2.create(linearSpeed, orientation)

		var angularVelocity : float
		if (Math.abs(angleBetweenVectors) <= SLOW_DOWN_ANGLE)
			angularVelocity = (angleBetweenVectors * maxAngular / SLOW_DOWN_ANGLE) as float
		else
			angularVelocity = (Math.signum(angleBetweenVectors) * maxAngular) as float

		return new ActionQuery(emitter, MotionBehavior::KINEMATIC, linearVelocity, angularVelocity)
	}
}
