/** 
 * MIT License
 * 
 * Copyright (c) 2017 Fouss
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
package fr.utbm.info.ia51.crowdproject.^agent

import fr.utbm.info.ia51.crowdproject.motion.KinematicWanderAlgorithm
import fr.utbm.info.ia51.crowdproject.motion.MotionBehavior
import fr.utbm.info.ia51.crowdproject.motion.ActionQuery
import fr.utbm.info.ia51.crowdproject.motion.SteeringWanderAlgorithm
import fr.utbm.info.ia51.crowdproject.motion.WanderAlgorithm
import io.sarl.core.Destroy
import io.sarl.core.Logging
import java.util.List
import fr.utbm.info.ia51.crowdproject.motion.SeekAlgorithm
import fr.utbm.info.ia51.crowdproject.motion.KinematicSeekAlgorithm
import fr.utbm.info.ia51.crowdproject.motion.SteeringSeekAlgorithm
import fr.utbm.info.ia51.crowdproject.motion.FleeAlgorithm
import fr.utbm.info.ia51.crowdproject.motion.KinematicFleeAlgorithm
import fr.utbm.info.ia51.crowdproject.motion.SteeringFleeAlgorithm
import fr.utbm.info.ia51.crowdproject.environment.HamsterBody
import fr.utbm.info.ia51.crowdproject.motion.CryType

/**
 * The hamster agent
 */
agent Hamster extends Animat {
	var wanderBehavior : WanderAlgorithm
	var seekBehavior : SeekAlgorithm
	var fleeBehavior : FleeAlgorithm

	uses Logging, EnvironmentMotion
	
	override initializationMethod(params : List<Object>) : void {
		info("New hamster spawned.")
		setSkill(new DefaultEnvironmentMotion, typeof(EnvironmentMotion))
		
		super.initializationMethod(params)
		
		if (this.motionBehavior == MotionBehavior::KINEMATIC) {
			this.wanderBehavior = new KinematicWanderAlgorithm
			this.seekBehavior = new KinematicSeekAlgorithm
			this.fleeBehavior = new KinematicFleeAlgorithm
		} else if (this.motionBehavior == MotionBehavior::STEERING) {
			this.wanderBehavior = new SteeringWanderAlgorithm
			this.seekBehavior = new SteeringSeekAlgorithm
			this.fleeBehavior = new SteeringFleeAlgorithm
		}
	}
	
	on Destroy {
		info("A hamster has been killed")
	}
	
	on Perception [it.agentId == this.agentId] {
		synchronized(this) {
			var firstSeen = occurrence.objects.findFirst[HamsterBody.isInstance(it)]
			var computedMotion : ActionQuery
			
			// If the target has been perceived
			if (firstSeen !== null) {
				computedMotion = this.seekBehavior.run(
				occurrence.agentId,
				occurrence.agentBody.orientation,
				occurrence.agentBody.position.to(firstSeen.position),
				occurrence.agentBody.maxLinear,
				occurrence.agentBody.maxAngular
				)
			} else {
				computedMotion = this.wanderBehavior.run(
					occurrence.agentId,
					occurrence.agentBody.orientation,
					occurrence.agentBody.maxLinear,
					occurrence.agentBody.maxAngular
				)
			}
			computedMotion.cry = CryType.POSITIVE
			
			move(occurrence.agentId, occurrence.source, occurrence.time, computedMotion)
		}
	}
}
